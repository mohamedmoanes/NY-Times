plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.moanes.nytimes"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            def proguard = true
            minifyEnabled proguard
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "SERVER_URL", '"https://api.nytimes.com/svc/"'
        }

        debug {
            def proguard = false
            minifyEnabled proguard
            shrinkResources proguard
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "SERVER_URL", '"https://api.nytimes.com/svc/"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies { configuration ->

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    androidX(configuration)
    lifecycle(configuration)
    retrofit(configuration)
    coroutines(configuration)
    koin(configuration)
    progressBar(configuration)
    imageLoader(configuration)
    dimens(configuration)

    implementation 'com.github.NightWhistler:HtmlSpanner:0.4'


    testImplementation 'android.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
    testImplementation "io.mockk:mockk:1.10.0"
// For espresso test cases
    androidTestImplementation ('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

// For recycler view actions
    androidTestImplementation ('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

// For espresso activity rule
    androidTestImplementation "com.android.support.test:rules:1.0.2"
    androidTestImplementation "com.android.support.test:runner:1.0.2"

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}


private static void androidX(configuration) {
    def version = "1.0.0"

    configuration.implementation "androidx.legacy:legacy-support-v4:$version"
    configuration.implementation "androidx.cardview:cardview:$version"
    configuration.implementation "androidx.recyclerview:recyclerview:1.2.0-beta01"
    configuration.implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    configuration.implementation 'androidx.appcompat:appcompat:1.2.0'
    configuration.implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    configuration.implementation "com.google.android.material:material:1.3.0"

}

private static void lifecycle(configuration) {
    def lifecycle_version = "2.3.0"

    configuration.implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    configuration.implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    configuration.implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    configuration.implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
}

private static void retrofit(configuration) {
    def retrofit_version = "2.9.0"
    def okhttp_version = "4.9.0"
    configuration.implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    configuration.implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    configuration.implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    configuration.debugImplementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    configuration.debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
}

private static void coroutines(configuration) {
    def coroutines_version = "1.4.2"
    configuration.implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    configuration.implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
}

private static void koin(configuration) {
    def koin_version = "2.2.1"
    configuration.implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    configuration.testImplementation "org.koin:koin-test:$koin_version"
}
private static void progressBar(configuration) {
    configuration.implementation 'com.wang.avi:library:2.1.3'
}
private static void imageLoader(configuration) {
    configuration.implementation 'com.squareup.picasso:picasso:2.71828'
}
private static void dimens(configuration) {
    configuration.implementation 'com.intuit.sdp:sdp-android:1.0.6'
    configuration.implementation 'com.intuit.ssp:ssp-android:1.0.6'
}